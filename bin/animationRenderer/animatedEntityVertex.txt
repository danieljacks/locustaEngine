#version 150

const int MAX_JOINTS = 50;//max joints allowed in a skeleton
const int MAX_WEIGHTS = 3;//max number of joints that can affect a vertex

in vec3 in_position;
in vec2 in_textureCoords;
in vec3 in_normal;
in ivec3 in_jointIndices;
in vec3 in_weights;

out vec2 pass_textureCoords;
out vec3 pass_normal;
out float visibility;
out vec3 toLightVector[25];
out vec3 toCameraVector;

uniform mat4 jointTransforms[MAX_JOINTS];
uniform float fogDensity;
uniform float fogGradient;

uniform mat4 transformationMatrix;
uniform vec2 textureOffset;
uniform vec4 plane;
uniform float numberOfRows;
uniform int lightsCount;
uniform vec3 lightPosition[25];
uniform mat4 projectionMatrix;
uniform mat4 viewMatrix;

void main(void){
	
	vec4 totalLocalPos = vec4(0.0);
	vec4 totalNormal = vec4(0.0);
	
	
	for(int i=0;i<MAX_WEIGHTS;i++){
		mat4 jointTransform = jointTransforms[in_jointIndices[i]];
		vec4 posePosition = jointTransform * vec4(in_position, 1.0);
		totalLocalPos += posePosition * in_weights[i];
		vec4 worldNormal = jointTransform * vec4(in_normal, 0.0);
		totalNormal += worldNormal * in_weights[i];
	}
	
	vec4 worldPosition = transformationMatrix * totalLocalPos;
	gl_ClipDistance[0] = dot(worldPosition, plane);
	vec4 positionRelativeToCam = viewMatrix * worldPosition;
	gl_Position = projectionMatrix * positionRelativeToCam;
	pass_normal = (transformationMatrix * totalNormal).xyz;
	pass_textureCoords = in_textureCoords;
	for(int j=0;j<lightsCount;j++){
		toLightVector[j] = lightPosition[j] - worldPosition.xyz;
	}
	toCameraVector = (inverse(viewMatrix) * vec4(0.0,0.0,0.0,1.0)).xyz - worldPosition.xyz;
	float fogDistance = length(positionRelativeToCam.xyz);
	visibility = exp(-pow((fogDistance*fogDensity),fogGradient));
	visibility = clamp(visibility,0.0,1.0);

}